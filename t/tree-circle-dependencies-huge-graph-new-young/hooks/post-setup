#!/usr/bin/perl

use strict;
use warnings;

use constant WIDTH => 20;
use constant DEPTH => WIDTH;

use constant NOT_READY => WIDTH * DEPTH;

use lib "$ENV{'TEST_ROOT'}/perl-lib";
use TestLib;

# Create an WIDTH x DEPTH matrix of packages
# - package depend directly on the package in the column "above" it (if any)
#
#  That is M[i][j] depends on M[i - 1][j] if i > 0 (or i > 1 of 1-index based notation :P)
#
# The name of each package is src-i-j (where i is the row and j the column of the package)
#
# Secondly, WIDTH/2 of each row are in a circular dependency - the rest depends on each other
# in a line ending in the circle of the same row.

my ($rundir) = @ARGV;
{
    my @pkgs = ();
    my $today = int(time / (3600 * 24));
    my %dates = ();
    open my $ext, '>', "$rundir/expected" or die "opening expected: $!";
    for my $i (0..DEPTH-1) {
        for my $j (0..WIDTH-1) {
            push @pkgs, ["src-$i-$j", '1.0-2'];
            print $ext "src-$i-$j 1.0-2 i386\n";
            print $ext "src-$i-$j 1.0-2 source\n";
        }
    }
    close $ext or die "closing expected: $!";

    for my $i (0..NOT_READY-1) {
        push @pkgs, ["young-$i", "1.0-2"];
        $dates{"young-$i/1.0-2"} = $today;
    }

    TestLib::gen_dates_urgencies ("$rundir/var/data/testing", \@pkgs, \%dates);
}


foreach my $data (['testing', '1.0-1'], ['unstable', '1.0-2']) {
    my ($suite, $version) = @$data;
    open my $pkgs, '>', "$rundir/var/data/$suite/Packages_i386" or
        die "open Packages_i386 ($suite): $!";
    open my $srcs, '>', "$rundir/var/data/$suite/Sources" or
        die "open Sources ($suite): $!";
    open my $dot, '>', "$rundir/debug-deps-$suite.dot" or
        die "open debug-deps-$suite.dot: $!";

    print $dot "digraph \"$suite\" {\n";

    for my $i (0..DEPTH-1) {
        for my $j (0..WIDTH-1) {
            my $dep = '';
            my $dj = $j - 1;
            $dj = int(WIDTH/2) - 1 unless $j;
            if ($i > 0) {
                $dep .= "src-" . ($i - 1) . "-$j (= $version), ";
                print $dot "    \"src-$i-$j\" -> \"src-" . ($i - 1) . "-$j\";\n";
            }
            $dep .= "src-$i-$dj (= $version)";

            print $dot "    \"src-$i-$j\" -> \"src-$i-$dj\";\n";

            print $pkgs <<EOF ;
Package: src-$i-$j
Source: src-$i-$j
Version: $version
Maintainer: The R-Team <debian-release\@lists.debian.org>
Depends: $dep
Architecture: i386
Section: devel

EOF
            print $srcs <<EOF ;
Package: src-$i-$j
Binary: src-$i-$j
Version: $version
Maintainer: The R-Team <debian-release\@lists.debian.org>
Section: devel

EOF
        }

        print $dot "    { rank=same; \"" . join ('" "', map { "src-$i-$_" } 0..WIDTH-1) . "\"}\n";
    }

    # Add some "new" packages that are too young
    if ($suite eq 'unstable') {
        for my $i (0..NOT_READY-1) {
            my $x = int($i / DEPTH);
            my $y = $i % WIDTH;
            my $o = "src-$x-$y";
            my $dep = "$o (= $version)";
            print $dot "    \"young-$i\" -> \"$o\"\n";
            print $pkgs <<EOF ;
Package: young-$i
Source: young-$i
Version: $version
Maintainer: The R-Team <debian-release\@lists.debian.org>
Depends: $dep
Architecture: i386
Section: devel

EOF
            print $srcs <<EOF ;
Package: young-$i
Binary: young-$i
Version: $version
Maintainer: The R-Team <debian-release\@lists.debian.org>
Section: devel

EOF
        }
    }

    print $dot "\n}\n";

    close $dot or die "close debug-deps-$suite.dot: $!";
    close $pkgs or die "close Packages_i386 ($suite): $!";
    close $srcs or die "close Sources ($suite): $!";
}

