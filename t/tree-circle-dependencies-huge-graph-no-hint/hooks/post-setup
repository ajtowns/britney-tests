#!/usr/bin/perl

use strict;
use warnings;

use constant WIDTH => 20;
use constant DEPTH => WIDTH;

use lib "$ENV{'TEST_ROOT'}/perl-lib";
use TestLib;

# Create an WIDTH x DEPTH matrix of packages
# - all packages in a given row are in a circular dependency
#   - M[i][j] depends on M[i][j-1] and it wraps around (M[i][0] depends on M[i][WIDTH-1])
# - package depend directly on the package in the column "above" it (if any)
#
#  That is M[i][j] depends on M[i - 1][j] if i > 0 (or i > 1 of 1-index based notation :P)
#
# The name of each package is src-i-j (where i is the row and j the column of the package)

my ($rundir) = @ARGV;
{
    my @pkgs = ();
    open my $ext, '>', "$rundir/expected" or die "opening expected: $!";
    for my $i (0..DEPTH-1) {
        for my $j (0..WIDTH-1) {
            push @pkgs, ["src-$i-$j", '1.0-2'];
            print $ext "src-$i-$j 1.0-2 i386\n";
            print $ext "src-$i-$j 1.0-2 source\n";
        }
    }
    close $ext or die "closing expected: $!";
    TestLib::gen_dates_urgencies ("$rundir/var/data/testing", \@pkgs);
}


foreach my $data (['testing', '1.0-1'], ['unstable', '1.0-2']) {
    my ($suite, $version) = @$data;
    open my $pkgs, '>', "$rundir/var/data/$suite/Packages_i386" or
        die "open Packages_i386 ($suite): $!";
    open my $srcs, '>', "$rundir/var/data/$suite/Sources" or
        die "open Sources ($suite): $!";

    for my $i (0..DEPTH-1) {
        for my $j (0..WIDTH-1) {
            my $dep = '';
            $dep .= "src-" . ($i - 1) . "-$j (= $version), " if ($i > 0);
            $dep .= "src-$i-" . ($j - 1) . " (= $version)" if $j;
            $dep .= "src-$i-" . (WIDTH-1) . " (= $version)" unless $j;
            
            print $pkgs <<EOF ;
Package: src-$i-$j
Source: src-$i-$j
Version: $version
Maintainer: The R-Team <debian-release\@lists.debian.org>
Depends: $dep
Architecture: i386
Section: devel
Description: nantoka nantoka

EOF
            print $srcs <<EOF ;
Package: src-$i-$j
Binary: src-$i-$j
Version: $version
Maintainer: The R-Team <debian-release\@lists.debian.org>
Section: devel

EOF
        }
    }

    close $pkgs or die "close Packages_i386 ($suite): $!";
    close $srcs or die "close Sources ($suite): $!";
}

